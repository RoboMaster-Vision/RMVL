# =====================================================================================
# Python 代码生成模块，包含以下功能：
#
#   1. rmvl_generate_python: 根据给定目标及对应的接口文件，生成 Python 代码
#
# =====================================================================================

function(rmvl_generate_python _name)
  # judge if the person.i file exists
  if(NOT EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/misc/python/${_name}.i)
    message(FATAL_ERROR "${CMAKE_CURRENT_SOURCE_DIR}/misc/python/${_name}.i not found.")
  endif()
  set_property(
    SOURCE misc/python/${_name}.i
    PROPERTY CPLUSPLUS ON
  )
  set(module_name rmvl_${_name})
  set(pymodule_name rmvl_${_name}_py)
  # add flags to build_${_name}_def.i if 'ARGN' is not empty
  file(WRITE ${CMAKE_CURRENT_SOURCE_DIR}/misc/python/build_${_name}_def.i "/* This file is generated by CMake, DO NOT MODIFY! */\n")
  foreach(flag ${ARGN})
    file(APPEND ${CMAKE_CURRENT_SOURCE_DIR}/misc/python/build_${_name}_def.i "#define ${flag}\n")
  endforeach()
  # generate python module
  swig_add_library(
    ${pymodule_name}
    LANGUAGE python
    SOURCES misc/python/${_name}.i
  )
  target_include_directories(
    ${pymodule_name}
    PRIVATE misc/python ${Python3_INCLUDE_DIRS}
  )
  target_link_libraries(
    ${SWIG_MODULE_${pymodule_name}_REAL_NAME}
    ${module_name} ${Python3_LIBRARIES}
  )
  set_target_properties(
    ${pymodule_name} PROPERTIES
    LIBRARY_OUTPUT_DIRECTORY ${CMAKE_SWIG_OUTDIR}
  )
  # export to __init__.py
  file(APPEND ${CMAKE_SWIG_OUTDIR}/__init__.py "from .rm_${_name} import *\n")

  # install python module
  install(
    FILES ${CMAKE_SWIG_OUTDIR}/rm_${_name}.py
          ${CMAKE_SWIG_OUTDIR}/_${pymodule_name}.so
    DESTINATION ${CMAKE_INSTALL_PREFIX}/${RMVL_PYTHON_INSTALL_SUFFIX}
  )
endfunction()

